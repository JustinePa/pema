#!/usr/bin/env bds

# This is P.E.M.A!  A metabarcoding pipenline for environmental DNA samples.
# Author: Haris Zafeiropoulos 
# date: 2017-2018
# This script is meant for the Singularity image version of P.E.M.A.

include "modules/initialize"
include "modules/preprocess"
include "modules/clustering"
include "modules/taxAssignment"
include "modules/postAssignment"
include "modules/phyloseq"


# Read parameters file for the first time! 
string parameterFilePath = "/mnt/analysis/"
parameterFilePath.chdir()
string f = "parameters.tsv"
string{} paramsFirstAssignment =  readParameterFile(f)


###############################################################
#                 0. Initialize analysis                      #
###############################################################
string{} globalVars = initializeAnalysis(paramsFirstAssignment)
wait

# If there are Trimlogs in the rawData file, PEMA does not run as it should. Hence, we remove them if there are any!
# I re-run this command because of FastQC and the way it reacts in multiple runs over the same folders
globalVars{'dataPath'}.chdir() ; 
sys if [ $(find '$globalVars{'dataPath'}' -name 'TrimLog*') ] ; then  rm TrimLog* ; fi

buildDirectories(paramsFirstAssignment, globalVars)

# IMPORTANT! Module to train classifiers is enabled through this statement
if ( paramsFirstAssignment{'custom_ref_db'} == 'Yes' ){
  string{} initGlobalVars = globalVars
  wait
  string{} globalVars = trainClassifier(paramsFirstAssignment, initGlobalVars)
}
wait

###############################################################
#              PRE-PROCESSING
###############################################################

# With the original approach
if ( paramsFirstAssignment{'preprocess'} == 'original' ) {

   # * Quality Control
   qualityControl(paramsFirstAssignment, globalVars)
   wait

   # * Trimming
   parameterFilePath.chdir()
   string g = "parameters.tsv"
   string{} paramsTrimmoStep =  readParameterFile(g)

   globalVars{'dataPath'}.chdir()

   # If ITS as marker gene, remove length variablity with cutadat
   if ( (paramsTrimmoStep{'gene'} == 'gene_ITS') && (paramsTrimmoStep{'cutadapt'} == 'Yes') ) {
      cutadaptForIts(paramsTrimmoStep, globalVars)
   }

   # Trim fastq files using Trimmomatic
   trimSeqs(paramsTrimmoStep, globalVars)
   wait

   # * Sequence adjustment
   parameterFilePath.chdir()
   string h = "parameters.tsv"
   string{} paramsBayesHammerStep =  readParameterFile(h)
   
   adjustSeqs(paramsBayesHammerStep, globalVars)
   wait

   # * Merging the paired-end samples
   parameterFilePath.chdir()
   string i = "parameters.tsv"
   string{} paramsSpadesMerging = readParameterFile(i)

   merging(paramsSpadesMerging, globalVars)
   wait

} else {
   # With fastp
   if ( (paramsFirstAssignment{'gene'} == 'gene_ITS') && ( paramsFirstAssignment{'cutadapt'} == 'Yes') ) {
      cutadaptForIts(paramsFirstAssignment, globalVars)
   }
   preprocessWithFastp(paramsFirstAssignment, globalVars)
   wait
}


################################################################
#          5. Dereplicate sequence samples                     #
################################################################

################################################################
#      Build subdirectories according to the params file       #
################################################################

parameterFilePath.chdir()
string j = "parameters.tsv"
string{} paramsDereplication =  readParameterFile(j)

buildDirectories(paramsDereplication, globalVars)

wait

println('Pema is at the dereplication step; this may take a while.')
if ( paramsDereplication{'clusteringAlgo'} == 'algo_Swarm' ) {

   println("Dereplication for the Swarm case to infer clusters.")
   swarmDereplicate(paramsDereplication, globalVars)
   wait


} else {

   println("Dereplications for the case of VSEARCH.")
   globalVars = obiDereplicate(paramsDereplication, globalVars)

   wait

   println("PEMA will merge reads from all files in one.")
   mergeFilesNonSwarm(paramsDereplication, globalVars)

   wait

}


string clustering = globalVars{'outputPoint'} + "/clustering.chp"
checkpoint clustering

################################################################
#           6. OTUs clustering / ASVs inference                #
################################################################

println('All the first steps are done! clustering is about to start!')
parameterFilePath.chdir()
string k = "parameters.tsv"
string{} paramsOfTable =  readParameterFile(k)

if ( paramsDereplication{'clusteringAlgo'} == 'algo_vsearch' ) {

   clusteringVsearch(paramsOfTable, globalVars)

} else if (paramsDereplication{'clusteringAlgo'} == 'algo_Swarm') {

   clusteringSwarm(paramsOfTable, globalVars)

}

println("The OTUs clustering / ASVs inference step has been completed.")
string taxAssign = globalVars{'outputPoint'} + "/taxAssign.chp"
checkpoint taxAssign

################################################################
#                7. Taxonomic assignment                       #
################################################################

parameterFilePath.chdir()
string l = "parameters.tsv"
string{} paramsForTaxAssign =  readParameterFile(l)

if ( paramsForTaxAssign{'custom_ref_db'} != 'Yes'){

   if ( paramsForTaxAssign{'gene'} == 'gene_16S') {

      if (paramsForTaxAssign{'classifierAlgo'} == 'RDPClassifier') {

         println('Pema cannot use RDPClassifier for your marker gene. It will use CREST instead and the relative parameter will change accordingly.')
         
         globalVars{'classifierAlgo'}         = 'CREST'
         paramsForTaxAssign{'classifierAlgo'} = 'CREST'
         wait

      }

      if (paramsForTaxAssign{'taxonomyAssignmentMethod'} != 'phylogeny') {

         crestAssign(paramsForTaxAssign, globalVars)
         wait
         println("Alignment based taxonomy assignment using the CREST classifier has been completed.")

      } else {
   
            phylogenyAssign(paramsForTaxAssign, globalVars)
            wait
            println("Phylogeny based taxonomy assignment has been completed.")

      }

   } else if ( paramsForTaxAssign{'gene'} == 'gene_COI' ) {

      if ( paramsForTaxAssign{'classifierAlgo'} == 'CREST' ) {

         println('Pema cannot run COI data using the CREST classifier. It will use RDPClassifer instead and the relative parameter will change accordingly.')

         globalVars{'classifierAlgo'}         = 'RDPClassifier'
         paramsForTaxAssign{'classifierAlgo'} = 'RDPClassifier'

         wait

      }
      
      if (paramsForTaxAssign{'classifierAlgo'} == 'RDPClassifier') {

         rdpAssign(paramsForTaxAssign, globalVars)
         wait
         println("Alignment based taxonomy assignment using the RDPClassifier has been completed.")

      }

   } else if (paramsForTaxAssign{'gene'} == 'gene_18S') {

      if ( paramsForTaxAssign{'classifierAlgo'} == 'RDPClassifier' ) {

         println('Pema cannot use RDPClassifier for your marker gene. It will use CREST instead and the relative parameter will change accordingly.')

         globalVars{'classifierAlgo'}         = 'CREST'
         paramsForTaxAssign{'classifierAlgo'} = 'CREST'
         wait

      }
      
      if ( paramsForTaxAssign{'classifierAlgo'} == 'CREST' ) {
 
         crestAssign(paramsForTaxAssign, globalVars)
         wait
         println("Alignment based taxonomy assignment using the CREST classifier has been completed.")

      }

   } else if (paramsForTaxAssign{'gene'} == 'gene_ITS') {

      if ( paramsForTaxAssign{'classifierAlgo'} == 'RDPClassifier' ) {

         println('Pema cannot use RDPClassifier for your marker gene. It will use CREST instead and the relative parameter will change accordingly.')

         globalVars{'classifierAlgo'}         = 'CREST'
         paramsForTaxAssign{'classifierAlgo'} = 'CREST'
         wait
      } 
      
      if ( paramsForTaxAssign{'classifierAlgo'} == 'CREST' ) {
 
         crestAssign(paramsForTaxAssign, globalVars)
         wait
         println("Alignment based taxonomy assignment using the CREST classifier has been completed.")

      }

   } else if ( paramsForTaxAssign{'gene'} == 'gene_12S' ) {

      if ( paramsForTaxAssign{'classifierAlgo'} == 'CREST' ) {

         println('Pema cannot run 12S data using the CREST classifier. It will use RDPClassifer instead and the relative parameter will change accordingly.')

         globalVars{'classifierAlgo'}         = 'RDPClassifier'
         paramsForTaxAssign{'classifierAlgo'} = 'RDPClassifier'

         wait

      }
      
      if (paramsForTaxAssign{'classifierAlgo'} == 'RDPClassifier') {

         rdpAssign(paramsForTaxAssign, globalVars)
         wait
         println("Alignment based taxonomy assignment using the RDPClassifier has been completed.")

      }
   }

} else if ( paramsForTaxAssign{'custom_ref_db'} == 'Yes') {

   if ( paramsForTaxAssign{'classifierAlgo'} == 'RDPClassifier' ) {

      rdpAssign(paramsForTaxAssign, globalVars)

   } else if ( paramsForTaxAssign{'classifierAlgo'} == 'CREST' ) {

      crestAssign(paramsForTaxAssign, globalVars)

   } else {

      println('The classifier you chose is not available in the Pema framework.')
   }

}
println("The taxonomy assignment step has been completed.")

# Check for running the ncbi-taxonomist tool
if ( paramsForTaxAssign{'getNCBITaxId'} == 'Yes'){

    ncbiTaxonomyMapping(paramsForTaxAssign, globalVars)

    println("The assignments on the finalTable.tsv file have been linked to NCBI Taxonomy ids.")
}

string communAnal = globalVars{'outputPoint'} + "/communAnal.chp"
checkpoint communAnal

################################################################
#                8. Biodiversity analysis                      #
################################################################

parameterFilePath.chdir()
string ca = "parameters.tsv"
string{} paramsForCA =  readParameterFile(ca)


if ( paramsForCA{'phyloseq'} == 'Yes') {

   communityAnalysis(paramsForCA, globalVars)

}

wait

################################################################
#                Pema has been concluded                      #
################################################################

###  Make a file where i will move all the checkpoints
parameterFilePath.chdir() ;

perSampleGeneral(paramsForCA, globalVars)
wait
if ( globalVars{'classifierAlgo'} == 'CREST') {
    CrestPerSample(paramsForCA, globalVars)
} else if ( globalVars{'classifierAlgo'} == 'RDPClassifier') {
    println("Get info per sample for RDPClassifier.")
    RDPClassifierPerSample(paramsForCA, globalVars)
}
wait

if ( paramsForCA{'emptyCheckpoints'} == 'Yes' ) {
   string checkpointsDir = 'checkpoints_for_' + paramsForCA{'outputFolderName'}
   globalVars{'outputFilePath'}.chdir()
   checkpointsDir.mkdir()
   globalVars{'outputPoint'}.chdir()   
   sys mv *.chp $globalVars{'outputFilePath'}/$checkpointsDir
}
wait

if ( (paramsForCA{'gene'} == 'gene_ITS') && (paramsForCA{'cutadapt'} == 'Yes') ) {

    globalVars{'dataPath'}.chdir()
    
    sys mkdir data_after_cutadapt 
    sys mv *fastq.gz data_after_cutadapt/
    sys mv data_after_cutadapt/ ..

    sys mv ../initialData/* .
    sys rmdir ../initialData

}
println('----------------------------------------------------------')

# make a copy of 'parameters.tsv' file to the output folder.
# this way the user will be able to get the parameters he used for analysis, anytime!
sys cp $globalVars{'parameterFilePath'}/parameters.tsv $globalVars{'outputFilePath'}/parameters0f.$globalVars{'analysisName'}.tsv

println('Pema has been completed successfully. Let biology start!')
println('Thanks for using Pema.')


